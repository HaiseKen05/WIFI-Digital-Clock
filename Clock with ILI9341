#include <WiFi.h>
#include "time.h"
#include "esp_sntp.h"
#include <TFT_eSPI.h>

TFT_eSPI tft = TFT_eSPI();  // TFT instance

// Wi-Fi Credentials
const char *ssid = "ESP32-AP";
const char *password = "ESP32-Connect";

// NTP Configuration
const char *ntpServer1 = "pool.ntp.org";
const char *ntpServer2 = "time.nist.gov";
const long gmtOffset_sec = 3600 * 8; // GMT+8
const int daylightOffset_sec = 0;

// Static location
const char *location = "Cebu, Philippines";

// Buffers
char lastDisplayedDate[20] = "";
char lastDisplayedTime[20] = "";

// Display coordinates
#define DATE_X  90
#define DATE_Y  40
#define TIME_X  90
#define TIME_Y  80
#define TIME_W  200
#define TIME_H  24
#define DATE_W  200
#define DATE_H  24

void printLocalTime() {
  struct tm timeinfo;

  if (!getLocalTime(&timeinfo)) {
    Serial.println("Failed to obtain time!");
    return;
  }

  // Format strings
  char currentDate[20];
  char currentTime[20];
  strftime(currentDate, sizeof(currentDate), "%b %d, %Y", &timeinfo);
  strftime(currentTime, sizeof(currentTime), "%H:%M:%S", &timeinfo);

  tft.setTextSize(2);
  tft.setTextColor(TFT_WHITE, TFT_BLACK);

  // Update date if changed
  if (strcmp(currentDate, lastDisplayedDate) != 0) {
    tft.fillRect(DATE_X, DATE_Y, DATE_W, DATE_H, TFT_BLACK);
    tft.setCursor(DATE_X, DATE_Y);
    tft.print(currentDate);
    strcpy(lastDisplayedDate, currentDate);
  }

  // Always update time (every second)
  if (strcmp(currentTime, lastDisplayedTime) != 0) {
    tft.fillRect(TIME_X, TIME_Y, TIME_W, TIME_H, TFT_BLACK);
    tft.setCursor(TIME_X, TIME_Y);
    tft.print(currentTime);
    strcpy(lastDisplayedTime, currentTime);
  }
}

void timeSyncCallback(struct timeval *t) {
  Serial.println("NTP Time Synchronized!");
}

void connectToWiFi() {
  Serial.printf("Connecting to Wi-Fi: %s ", ssid);
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWi-Fi Connected!");
}

void setup() {
  Serial.begin(115200);

  tft.init();
  tft.setRotation(1);
  tft.fillScreen(TFT_BLACK);

  tft.setTextSize(2);
  tft.setTextColor(TFT_WHITE, TFT_BLACK);
  tft.setCursor(40, 120);
  tft.println("Connecting Wi-Fi...");

  connectToWiFi();

  esp_sntp_servermode_dhcp(1);
  sntp_set_time_sync_notification_cb(timeSyncCallback);
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer1, ntpServer2);

  // Static UI
  tft.fillScreen(TFT_BLACK);
  tft.setCursor(10, DATE_Y);
  tft.print("Date: ");
  tft.setCursor(10, TIME_Y);
  tft.print("Time: ");
  tft.setCursor(10, 120);
  tft.print("Location:");
  tft.setCursor(10, 150);
  tft.println(location);
}

void loop() {
  delay(1000);
  printLocalTime();
}
